# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# AWS completions
if [[ -a /usr/local/share/zsh/site-functions/_aws ]]
then
  source /usr/local/share/zsh/site-functions/_aws
fi

unset config_files

[ -f /opt/boxen/env.sh ] && source /opt/boxen/env.sh

if [ `uname` = "Darwin" ]
then
  . `brew --prefix`/Cellar/z/1.8/etc/profile.d/z.sh
fi

# export PGHOST=/var/pgsql_socket

if hash archey 2>/dev/null;
then
  archey -c
fi
# doge
# cowsay -f ntlk cooking with gas!
# cowsay -f markconnor "sup bb"

export ACKRC=".ackrc"

. $ZSH/zsh/h.sh

BASE16_SHELL="$ZSH/zsh/base16-ocean-dark.sh"

[[ -s $BASE16_SHELL ]] && source $BASE16_SHELL

### Added by the Heroku Toolbelt
export PATH="/usr/local/heroku/bin:$PATH"

# $(boot2docker shellinit)
